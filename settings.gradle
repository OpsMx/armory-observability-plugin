pluginManagement {
    repositories {
        maven {
            url 'https://armory.jfrog.io/armory/gradle-plugins/'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
        mavenLocal()
        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.namespace == 'spinnaker') {
                useModule('com.netflix.spinnaker.gradle:spinnaker-gradle-project:7.0.1')
            }
        }
    }
}

rootProject.name = 'armory-observability-plugin'

include(
    'clouddriver',
    'common',
    'echo',
    'fiat',
    'front50',
    'gate',
    'igor',
    'kayenta',
    'orca',
    'rosco',
)

def setBuildFile(project) {
    project.buildFileName = "${project.name}.gradle"
    project.children.each {
        setBuildFile(it)
    }
}

rootProject.children.each {
    setBuildFile(it)
}
